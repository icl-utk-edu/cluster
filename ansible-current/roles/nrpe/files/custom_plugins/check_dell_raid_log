#!/usr/bin/perl

use strict;
use warnings;
use lib '/usr/lib64/nagios/plugins';
use utils '%ERRORS';
use Date::Parse 'str2time';

my ($file) = glob('/var/log/lsi_*.log');
unless(defined $file){
  print "WARNING: No controller log file found\n";
  exit $ERRORS{'WARNING'};
  }
my %bad = map {/^(\S+)\s(\d+)/;$1=>$2} <DATA>;

my $badness = 0;
my $msg_worst;
open(IN, $file) or die $!;
while(<IN>){
  if(/^T0:/){
    undef $msg_worst;
    $badness = 0;
    next;
    }
  my ($date, $code, $msg) = /^(\d+\/\d+\/\d+)\s+.+?:\s+EVT.+?:\s+(\d+)=(.+)$/;
  my $time = str2time($date) || 0;
  next if time()-$time>3600*24*7;
  next unless defined $code and $bad{$code};
  if($bad{$code} >= $badness){
    $badness = $bad{$code};
    $msg =~ s/\r//;
    $msg_worst = $msg;
  }
  }
close IN;

my @level = qw(OK WARNING CRITICAL);
$msg_worst ||= 'No errors';
print "$level[$badness]: $msg_worst\n";
exit $ERRORS{ $level[$badness] };

__DATA__
10	2 Controller cache discarded due to memory/battery problems
47	1 Background Initialization corrected medium error (VD 00/0 at 5df8, PD 04(e0x20/s4) at 5df8)
#81	1 State change on VD 01/1 from OPTIMAL(3) to PARTIALLY DEGRADED(1)
87	2 Error on PD 09(e0x20/s9) (Error f0)
93	1 Patrol Read corrected medium error on PD 00(e0x20/s0) at 2c0dec4
96	2 Predictive failure: PD 00(e0x20/s0)
97	1 Puncturing bad block on PD 00(e0x20/s0) at 2597fe25
102	2 Rebuild failed on PD 09(e0x20/s9) due to target drive error
110	1 Corrected medium error during recovery on PD 04(e0x20/s4) at 19f0
111	2 Unrecoverable medium error during recovery on PD 01(e0x20/s1) at 2597fe25
#113	1 Unexpected sense: Encl PD 20 Path 5b4ba0b02cd37f00, CDB: 1c 01 00 00 20 00, Sense: 6/29/00
#114	1 State change on PD 03(e0x20/s3) from UNCONFIGURED_GOOD(0) to UNCONFIGURED_BAD(1)
131	2 Unable to access device PD 03(e0x20/s3)
150	2 Battery needs replacement - SOH Bad
201	1 Single-bit ECC error: ECAR=13e3b998, ELOG=22000, ( More info in next line); warning threshold exceeded
202	2 Single-bit ECC error: ECAR=1ca89fb8, ELOG=22000, ( More info in next line); critical threshold exceeded
203	2 Single-bit ECC error: ECAR=18d3b018, ELOG=22000, ( More info in next line); further reporting disabled
238	2 PDs missing from configuration at boot
250	2 VD 00/0 is now PARTIALLY DEGRADED
251	2 VD 00/0 is now DEGRADED
257	2 PD missing: SasAddr=0x5000c50020dedfb5, ArrayRef=0, RowIndex=0x3, EnclPd=0x20, Slot=3.
267	2 Command timeout on PD 03(e0x00/s0) Path 5000c50020dedfb5, CDB: 1b 00 00 00 01 00
268	1 PD 03(e0x00/s0) Path 5000c50020dedfb5  reset (Type 03)
271	2 Uncorrectable medium error logged for VD 01/1 at 480ffa25 (on PD 00(e0x20/s0) at 2597fe25)
272	1 VD medium error corrected on VD 01/1 at 480ffa25
431	1 Battery charging was suspended due to high battery temperature
