#!/usr/bin/perl
#
# Create a USER-level user:
# user set name 3 monitor
# user set password 3 asldkfasldkf
# user enable 3
# chassis setaccess 1 3 link=on ipmi=on callin=on privilege=2
#
use strict;
use warnings;
use Newton;
use Data::Dumper;

Newton::sudo();

my $node = shift @ARGV;
my $db = Newton::db();
my $new_pw = Newton::bmc_passwd();
print "New BMC password is '$new_pw'\n";
my $old_pw  = $ENV{BMC_PASSWD} || Newton::get_passwd('Old BMC password');

$node ||= '';
my $systems = $db->selectall_arrayref(
	'SELECT system,ip FROM addresses WHERE system like ? and type="bmc"',
	undef, "$node\%",
    );

print "Changing BMC passwords for the following systems: ";
print join(' ', map {$_->[0]} @$systems);
print ".  Press <ENTER> to continue.\n";
<>;

for(@$systems){
  my ($system, $ip) = @$_;
  my $bmc = Newton::ipaddr($ip);
  my @param = ($system, $bmc, $old_pw);
  my ($channel, $uid) = find_ipmi_user_info(@param);
  unless(defined $uid){
    cmd("user enable 2", @param);
    cmd("user set name 2 root", @param);
    cmd("user priv 2 0x4 1", @param);
    ($channel, $uid) = find_ipmi_user_info(@param);
    unless(defined $uid){
       warn "No user ID found for root at $system";
       next;
    }
  }
  cmd("channel setaccess $channel $uid link=off callin=off ipmi=on privilege=0x4", @param);
  print cmd("user set password $uid $new_pw", @param);
}

sub find_ipmi_user_info {
  my ($system, $bmc, $old_pw) = @_;
  my ($channel, $uid);
  for(1..10){
    $channel = $_;
    my @info = cmd("user list $channel", $system, $bmc, $old_pw);
    ($uid) = map {$_->[0]}
                grep {$_->[1] eq 'root'}
                grep {$_->[5] =~ /administrator/i}
		grep { defined $_->[5] }
                map {chomp; [split /\s+/]} @info;
    last if defined $uid;
  }
  return ($channel, $uid);
}

sub cmd {
  my ($cmd, $system, $bmc, $password) = @_;
  my @data;
  if(defined $bmc and defined $password and $password ne ''){
    print "$system (bmc): $cmd\n";
    @data = Newton::ipmi($cmd, $bmc, $password);
  } else {
    print "$system (host): $cmd\n";
    @data = Newton::ssh("ipmitool $cmd", $system);
  }
  return @data;
}
