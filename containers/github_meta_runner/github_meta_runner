#!/usr/bin/perl

use strict;
use warnings;

github_meta_runner->new(80)->run();


package github_meta_runner;

use strict;
use warnings;
use HTTP::Server::Simple::CGI;
use base qw(HTTP::Server::Simple::CGI);
use JSON 'decode_json';


sub handle_request {
   my $self = shift;
   my $cgi  = shift;
   my ($message, $code) = response($cgi);
   print "HTTP/1.0 $code\r\n";
   print $cgi->header();
   print $message;
}

sub response {
   my $cgi = shift;
   my $raw = $cgi->param('payload') || return Log('payload error');
   my $payload = decode_json($raw) || return Log('decode error');
   my $org = $payload->{organization}->{login};
   my $action = $payload->{action};
   return(Log('ignoring'), 200) unless $action eq 'queued';
   my $job = $payload->{workflow_job};
   return(Log('job not found'), 501) unless defined $job and ref($job);
   my $labels = $job->{labels};
   return(Log('no labels found'), 501) unless defined $labels and ref($labels);
   my ($slurm_label) = grep /^slurm/, @$labels;
   return(Log('ignoring'), 200) unless defined $slurm_label;
   my $slurm_request = generate_slurm_request($slurm_label);
   my $ans = `sbatch $slurm_request /opt/github_ephemeral.sbatch $org $slurm_label 2>&1`;
   chomp $ans;
   if($ans =~ /Submitted batch job \d+/){
      return(Log("$ans ($slurm_request)"), 200);
   } else {
      return(Log("Error submitting job for ($slurm_request) ($ans)"), 501);
   }
}


sub Log {
   my $msg = shift;
   my $date = localtime();
   warn "[$date] $msg\n";
   return $msg;
}

sub generate_slurm_request {
   my (undef, $params) = split(/\:/, shift);
   my @list = split(/\+/, $params);
   my @gres = ('github');
   my @const = ('docker');
   for(@list){
      if(/gpu_(\w+)/){
	 push @gres, "gpu:$1";
      }
      if(/^(cpu_\w+)$/){
	 push @const, $1;
      }
   }
   my @out = ( '-C', join(',', @const) );
   push(@out, '--gres', join(',', @gres)) if @gres;
   return join(' ', @out);
}

1;
