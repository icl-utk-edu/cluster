#!/usr/bin/perl

# If the node has GPUs configured in Slurm, check that they look OK

use strict;
use warnings;
use lib '/usr/lib64/nagios/plugins';
use utils '%ERRORS';
use Data::Dumper;

my %funcs = (
	'nvidia' => \&gpu_nvidia,
	'amd' => \&gpu_amd,
	'intel' => \&gpu_intel,
	);

my $hostname = `hostname -s`;
my $config = `scontrol show node $hostname`;
$config =~ /Gres=(\S+)/s;
my @gres = split(',', $1);
for(@gres){
	my ($gputype, $count) = /gpu:(\w+):(\d+)/;
	next unless defined $gputype;
        my $func = $funcs{$gputype};
	my $ok = &$func($count);
	finish("CRITICAL", "$gputype not found") unless $ok;
}

finish('OK', "All GPUs verified");


sub gpu_nvidia {
	my $count = shift;
	my @gpus = `nvidia-smi -L`;
	return scalar(@gpus) == $count;
}

sub gpu_amd {
	my $count = shift;
	my @gpus = grep {/^card\d+,/}
                   `rocm-smi --showproductname --csv`;
	return scalar(@gpus) == $count;
}

sub gpu_intel {
	my $count = shift;
	my @gpus = grep {/Device #\d+/} `clinfo -l`;
	return scalar(@gpus) == $count;
}

sub finish {
  my ($code, $msg) = @_;
  print "GPUs $code: $msg\n";
  exit $ERRORS{$code};
  }
